ACTOR Flamethrower2 : BrutalWeapon
{
	Height 20
	Weapon.SelectionOrder 3200
	Weapon.AmmoUse 5
	Weapon.AmmoGive 50
	Weapon.AmmoType "Gas"
    Inventory.PickupSound "Flamer/Select"
    +WEAPON.BFG
	+WEAPON.NOAUTOAIM
	+WEAPON.NOAUTOFIRE
	+WEAPON.EXPLOSIVE
	Inventory.PickupMessage "You got the Flamethrower (Slot 9)"
	Tag "Flamethrower"
	DropItem "FlameThrowerSpawner"
	States
	{
	KickFlash://18 tics
		FLMT MNO 1
		FLMT P 15
		FLMT ONM 1
		Stop

	SlideFlashEnd://10 tics
		FLMT P 10
		FLMT ONM 1
		Stop

	Deselect:
	    FLMT A 0 A_StopSound(CHAN_WEAPON)
		FLMT A 0 A_TakeInventory("TossGrenade", 1)
		TNT1 A 0 A_TakeInventory("Unloading", 1)
		TNT1 A 0 A_TakeInventory("Reloading", 1)
		TNT1 A 0 A_TakeInventory("StartDualWield",1)
		FLMT MNOP 1
		TNT1 AAAAAAAAAAAAAAAAAA 0 A_Lower
		TNT1 A 1 A_Lower
		Wait

	Select:
		FLMT A 0 A_Giveinventory("GoSpecial",1)
		TNT1 A 0 A_TakeInventory("FistsSelected", 1)
		TNT1 A 0 A_TakeInventory("ShotgunSelected", 1)
		TNT1 A 0 A_TakeInventory("MinigunSelected", 1)
		TNT1 A 0 A_TakeInventory("SSGSelected", 1)
		TNT1 A 0 A_TakeInventory("RocketLauncherSelected", 1)
		TNT1 A 0 A_TakeInventory("GrenadeLauncherSelected", 1)
		TNT1 A 0 A_TakeInventory("PlasmaGunSelected", 1)
		TNT1 A 0 A_TakeInventory("RailGunSelected", 1)
		TNT1 A 0 A_TakeInventory("RevenantLauncherSelected", 1)
		TNT1 A 0 A_TakeInventory("SubMachineGunSelected", 1)
		TNT1 A 0 A_TakeInventory("BFG10KSelected", 1)
		TNT1 A 0 A_TakeInventory("BFGSelected", 1)
		TNT1 A 0 A_GiveInventory("FlameCannonSelected", 1)
		TNT1 A 0 A_TakeInventory("SawSelected", 1)
		FLMT A 0 A_Takeinventory("HasBarrel",1)
		FLMT A 0 A_TakeInventory("TossGrenade", 1)
		FLMT A 0 A_GunFlash
		TNT1 A 1 A_Raise
		TNT1 AAAAAAAAAAAA 0 A_Raise
		Goto SelectAnimation

	SelectAnimation:
		TNT1 A 0
		FLMT IJKL 1 A_JumpIfInventory("GoFatality", 1, "Steady")
		RLNG A 0 A_PlaySound("Flamer/Select", 5)
		RLNG A 0 A_JumpIfInventory("Reloading",1,"Reload")
		Goto Ready

	Ready:
	Ready3:
	Reload:
		FLMT A 0
		FLMT A 0 A_JumpIf(waterlevel > 1, "ReadyDry")
		FLMT A 0 A_JumpIfNoAmmo("ReadyDry")
		FLMT A 0 A_PlaySound("Flamer/idle", CHAN_WEAPON, 1, 1)
		FLMT ABC 1 BRIGHT A_WeaponReady
		FLMT A 0 A_JumpIfInventory("GoFatality", 1, "Steady")
        FLMT A 0 A_JumpIfInventory("Kicking",1,"DoKick")
        FLMT A 0 A_JumpIfInventory("Taunting",1,"Taunt")
		FLMT A 0 A_JumpIfInventory("Salute1", 1, "Salute")
		FLMT A 0 A_JumpIfInventory("Salute2", 1, "Salute")
		FLMT A 0 A_JumpIfInventory("TossGrenade",1,"TossGrenade")
		TNT1 A 0 A_JumpIfInventory("ExecuteDownedEnemy", 1, "DoExecution")
		FLMT A 0 A_JumpIfInventory("IsRunning",1,"StartSprint")
		Goto Ready3

	ReadyDry:
	    FLMT A 0 A_StopSound(CHAN_WEAPON)
		FLMT Q 1 A_WeaponReady
		FLMT A 0 A_JumpIfInventory("GoFatality", 1, "Steady")
        FLMT A 0 A_JumpIfInventory("Kicking",1,"DoKick")
        FLMT A 0 A_JumpIfInventory("Taunting",1,"Taunt")
		FLMT A 0 A_JumpIfInventory("Salute1", 1, "Salute")
		FLMT A 0 A_JumpIfInventory("Salute2", 1, "Salute")
		FLMT A 0 A_JumpIfInventory("TossGrenade",1,"TossGrenade")
		TNT1 A 0 A_JumpIfInventory("ExecuteDownedEnemy", 1, "DoExecution")
		FLMT A 0 A_JumpIfInventory("IsRunning",1,"StartSprint")
		Goto Ready3

	StartSprint:
		FLMT RST 1 A_WeaponReady
		SHTN A 0 A_Takeinventory("Zoomed",1)
        SHTN A 0 A_ZoomFactor(1.0)
		SHTN A 0 A_Takeinventory("ADSmode",1)
		RIFG A 0 A_SetCrosshair(0)
		SHTN A 0 A_Giveinventory("GoSpecial",1)
		TNT1 A 0 ACS_NamedExecuteAlways("BDSprint", 0, 0, 0, 0)//Makes player faster.

	Sprinting:
		FLMT U 1 offset(-9,34) A_SetPitch(pitch -0.5)
		FLMT U 1 offset(-6,36) A_SetPitch(pitch -0.5)
		FLMT U 1 offset(-3,38) A_SetPitch(pitch -0.5)
		FLMT U 1 offset(0,38) A_SetPitch(pitch -0.5)
		FLMT U 1 offset(3,36) A_SetPitch(pitch -0.5)
		FLMT U 1 offset(6,34) A_SetPitch(pitch -0.5)
		FLMT U 1 offset(9,32) A_SetPitch(pitch -0.5)
		TNT1 A 0 A_GiveInventory("UsedStamina", 3)
		SMGG A 0 A_WeaponReady(WRF_NOBOB)
		SMGG A 0 A_JumpIfInventory("Kicking",1,"DoKick")
        SMGG A 0 A_JumpIfInventory("Taunting",1,"Taunt")

		FLMT U 1 offset(9,34) A_SetPitch(pitch +0.5)
		FLMT U 1 offset(6,36) A_SetPitch(pitch +0.5)
		FLMT U 1 offset(3,38) A_SetPitch(pitch +0.5)
		FLMT U 1 offset(0,38) A_SetPitch(pitch +0.5)
		FLMT U 1 offset(-3,36) A_SetPitch(pitch +0.5)
		FLMT U 1 offset(-6,34) A_SetPitch(pitch +0.5)
		FLMT U 1 offset(-9,32) A_SetPitch(pitch +0.5)
		TNT1 A 0 A_GiveInventory("UsedStamina", 3)
		SMGG A 0 A_WeaponReady(WRF_NOBOB)
		SMGG A 0 A_JumpIfInventory("Kicking",1,"DoKick")
        SMGG A 0 A_JumpIfInventory("Taunting",1,"Taunt")

		PLAY A 0 ACS_NamedExecuteAlways("BDSprint", 0, 0, 0, 0)//Makes player faster.
		SMGG A 0 A_JumpIfInventory("IsRunning", 1, "Sprinting")
		Goto StopSprint

	StopSprint:
		FLMT TSR 1
		FLMT A 0 A_JumpIfInventory("PowerStrength", 1, 2)
		FLMT A 0 A_JumpIfInventory("UsedStamina", 100, "StopSprintTired")
		FLMT A 0 ACS_NamedExecuteAlways("BDTacticalSpeed", 0, 0, 0, 0)//Makes player slower.
		Goto Ready

	StopSprintTired:
		FLMT A 0 ACS_NamedExecuteAlways("BDTacticalSpeed", 0, 0, 0, 0)//Makes player slower.
		FLMT A 0 A_PlaySound("Tired", 2)
		Goto Ready

	DryFire:
	    FLMT Q 4 A_StopSound(CHAN_WEAPON)
		FLMT A 0 A_PlaySound("weapons/empty")
		Goto Ready+6

    Spawn:
        FLMT F -1
        Stop

   Fire:
        FLMT A 0
		FLMT A 0 A_JumpIf(waterlevel > 1, "DryFire")
		FLMT A 0 A_StopSound(CHAN_WEAPON)

	Hold:
		FLMT A 0 A_JumpIfNoAmmo("DryFire")
		TNT1 A 0 A_FireCustomMissile("Alerter",0,0)
        FLMT A 0 A_FireCustomMissile("FlamethrowerMissile", 0, 1, 0, 0, 0, 8)
		FLMT A 0 A_SpawnItemEx("PlayerMuzzle1",32,0,28)
		FLMT A 0 A_PlaySound("Flamer/loop", CHAN_WEAPON, 1, 1)
		FLMT DH 1 BRIGHT
		TNT1 A 0 A_FireCustomMissile("Alerter",0,0)
		FLMT A 0 A_FireCustomMissile("FlamethrowerMissileProper", 0, 1, 0, 0, 0, 8)
		FLMT A 0 A_SpawnItemEx("PlayerMuzzle1",32,0,28)
		FLMT EG 1 BRIGHT
		FLMT A 0 A_Refire
		FLMT A 0 A_PlaySound("Flamer/idle", CHAN_WEAPON, 1, 1)
		FLMT A 0 A_PlaySound("Flamer/stop", 5)
		Goto Ready+6
	}
}