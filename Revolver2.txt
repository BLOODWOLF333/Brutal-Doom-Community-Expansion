ACTOR Revolver2 : BrutalWeapon
{
	Weapon.SelectionOrder 1600
	Weapon.AmmoUse1 0
	Weapon.AmmoUse2 0
	Weapon.AmmoGive1 0
	Weapon.AmmoGive2 0
	Weapon.AmmoType1 "AmmoShell"
	Weapon.AmmoType2 "RevolverAmmo"
	Inventory.PickupMessage "$GOTREVOLVER"
    Inventory.PickupSound "Revolver/Hammer"
	Obituary "$OB_MPSHOTGUN"
    AttackSound "None"
    +WEAPON.NOAUTOAIM
	+WEAPON.NOAUTOFIRE
	+NOALERT
	+WEAPON.CHEATNOTWEAPON
	Scale 0.6
	Tag "Revolver"
	DropItem "RevolverSpawner"
	States
	{
	KickFlash://18 tics
		REVO VWX 1
		REVO X 12
		REVO XWV 1
		Stop

	SlideFlashEnd://10 tics
		REVO X 7
		REVO XWV 1
		Stop

    Spawn:
		REVP A -1
		Stop

	Ready3:
	Ready:
		REVO A 0 A_JumpIfInventory("PistolBurstFire", 1, "Ready2")
		REVO A 1 A_WeaponReady(WRF_ALLOWRELOAD)
        REVO A 0 A_JumpIfInventory("Kicking",1,"DoKick")
        REVO A 0 A_JumpIfInventory("Taunting",1,"Taunt")
        REVO A 0 A_JumpIfInventory("Reloading",1,"Reload")
		REVO A 0 A_JumpIfInventory("Salute1", 1, "Salute")
		REVO A 0 A_JumpIfInventory("Salute2", 1, "Salute")
		REVO A 0 A_JumpIfInventory("Unloading",1,"Unload")
		REVO A 0 A_JumpIfInventory("FiredPrimary",1,"Fire")
		REVO A 0 A_JumpIfInventory("StartDualWield",1,"DualWield")
		REVO A 0 A_JumpIfInventory("TossGrenade",1,"TossGrenade")
		TNT1 A 0 A_JumpIfInventory("ExecuteDownedEnemy", 1, "DoExecution")
		REVO A 0 A_JumpIfInventory("IsRunning",1,"StartSprint")
        Loop

	Ready2://Fan The Hammer Ready State
		REVA A 1 A_WeaponReady(WRF_ALLOWRELOAD)
        REVO A 0 A_JumpIfInventory("Kicking",1,"DoKick")
        REVO A 0 A_JumpIfInventory("Taunting",1,"Taunt")
        REVO A 0 A_JumpIfInventory("Reloading",1,"Reload")
		REVO A 0 A_JumpIfInventory("Salute1", 1, "Salute")
		REVO A 0 A_JumpIfInventory("Salute2", 1, "Salute")
		REVO A 0 A_JumpIfInventory("Unloading",1,"Unload")
		REVO A 0 A_JumpIfInventory("FiredPrimary",1,"Fire")
		REVO A 0 A_JumpIfInventory("StartDualWield",1,"DualWield")
		REVO A 0 A_JumpIfInventory("TossGrenade",1,"TossGrenade")
		TNT1 A 0 A_JumpIfInventory("ExecuteDownedEnemy", 1, "DoExecution")
		REVO A 0 A_JumpIfInventory("IsRunning",1,"StartSprint")
        Loop

	Deselect:
		REVO A 0
		REVO A 0 A_Takeinventory("Zoomed",1)
		REVO A 0 A_TakeInventory("TossGrenade", 1)
		REVO A 0 A_Takeinventory("ADSmode",1)
		REVO A 0 A_TakeInventory("PistolBurstFire",1)
        REVO A 0 A_ZoomFactor(1.0)
		REVO VWX 1
		TNT1 AAAAAAAAAAAAAAAAAA 0 A_Lower
		TNT1 A 1 A_Lower
		Wait

	Select:
		REVO A 0 A_Giveinventory("GoSpecial",1)
		TNT1 A 0 A_TakeInventory("FistsSelected", 1)
		TNT1 A 0 A_TakeInventory("ShotgunSelected", 1)
		TNT1 A 0 A_TakeInventory("MinigunSelected", 1)
		TNT1 A 0 A_TakeInventory("SSGSelected", 1)
		TNT1 A 0 A_TakeInventory("RocketLauncherSelected", 1)
		TNT1 A 0 A_TakeInventory("GrenadeLauncherSelected", 1)
		TNT1 A 0 A_TakeInventory("PlasmaGunSelected", 1)
		TNT1 A 0 A_TakeInventory("RailGunSelected", 1)
		TNT1 A 0 A_TakeInventory("RevenantLauncherSelected", 1)
		TNT1 A 0 A_GiveInventory("SubMachineGunSelected", 1)
		TNT1 A 0 A_TakeInventory("BFG10KSelected", 1)
		TNT1 A 0 A_TakeInventory("BFGSelected", 1)
		TNT1 A 0 A_TakeInventory("FlameCannonSelected", 1)
		TNT1 A 0 A_TakeInventory("SawSelected", 1)
		REVO A 0 A_Takeinventory("HasBarrel",1)
		REVO A 0 A_TakeInventory("TossGrenade", 1)
		REVO A 0 A_TakeInventory("Revolver", 1)
		REVO A 0 A_Takeinventory("StartDualWield",1)
		REVO A 0 A_TakeInventory("PistolBurstFire",1)
		A12G A 0 A_GunFlash
		TNT1 A 1 A_Raise
		TNT1 AAAAAAAAAAAA 0 A_Raise
		Goto SelectAnimation

	SelectAnimation:
		TNT1 A 0
		TNT1 A 0 A_JumpIfInventory("RevolverChangeAmmo", 1, "Reload")
		REVO UTS 1 A_JumpIfInventory("GoFatality", 1, "Steady")
		REVO KLMN 2 A_JumpIfInventory("GoFatality", 1, "Steady")
		TNT1 A 0 A_PlaySound("Revolver/Hammer", 4)
		REVO OPQR 2 A_JumpIfInventory("GoFatality", 1, "Steady")
		Goto Ready

    Fire:
        REVO A 0 A_Takeinventory("Reloading",1)
        REVO A 0 A_JumpIfInventory("RevolverAmmo",1,2)
        Goto NoAmmo
        TNT1 AAAA 0
		REVO A 0 A_JumpIfInventory("PistolBurstFire",1,"Fire2")
		REVO B 1
        REVO A 0 A_AlertMonsters
        REVO A 0 A_PlaySound("Revolver/Fire", 1)
		RIFF A 0 A_FireCustomMissile("DistantFireSoundShotgun", random(-1,1), 0, 0, -12, 0, random(-1,1))
        REVO A 0 A_SpawnItemEx("PlayerMuzzle1",32,0,28)
		TNT1 AAAAAAAAAAAAAAAA 0 BRIGHT A_FireCustomMissile("ShotgunParticles", random(-12,12), 0, 0, 0, 0, random(-9,9))
		TNT1 AAAAA 0 BRIGHT A_FireCustomMissile("ShotgunParticles2", random(-12,12), 0, 0, 0, 0, random(-9,9))
		TNT1 AAAAA 0 A_FireCustomMissile("DecorativeTracer", random(-3,3), 0, 0, 0, 0, random(-3,3))
		REVO A 0 A_Takeinventory("RevolverAmmo",1)
		REVO A 0 A_GiveInventory("SpentRound",1)
		REVO A 0 A_FireBullets (2.7, 2.8, 4, 13, "ShotgunPuff", FBF_NORANDOM)
		REVO A 0 A_FireBullets (2.7, 2.8, 5, 13, "ShotgunPuff2", FBF_NORANDOM)
		REVO A 0 A_JumpIfInventory("IsNOTTacticalClass", 1, 3)//Tactical Mode deals extra damage and recoil
		REVO A 0 A_FireBullets (2.7, 2.8, 5, 13, "ShotgunPuff2", FBF_NORANDOM)
		REVO A 0 A_SetPitch(pitch-1.8)
		REVO A 0 A_SetPitch(pitch-1.8)
		REVO C 1 BRIGHT
		TNT1 A 0 A_ZoomFactor(0.98)
		REVO D 1 BRIGHT
		TNT1 A 0 A_ZoomFactor(1.0)
		REVO E 1 A_SetPitch(pitch+0.6)
		REVO FG 1 A_SetPitch(pitch+0.6)
		REVO F 1
		REVO E 1
		REVO K 4
		REVO LMN 1
		TNT1 A 0 A_PlaySound("Revolver/Hammer", 4)
		REVO OPQR 1
		REVO A 1 A_JumpIfInventory("FiredPrimary",1,"Fire")
		REVO A 0 A_Refire
		Goto Ready3

	Fire2:
		REVA B 1
        REVO A 0 A_AlertMonsters
        REVO A 0 A_PlaySound("Revolver/Fire", 1)
		RIFF A 0 A_FireCustomMissile("DistantFireSoundShotgun", random(-1,1), 0, 0, -12, 0, random(-1,1))
        REVO A 0 A_SpawnItemEx("PlayerMuzzle1",32,0,28)
		TNT1 AAAAAAAAAAAAAAAA 0 BRIGHT A_FireCustomMissile("ShotgunParticles", random(-12,12), 0, 0, 0, 0, random(-9,9))
		TNT1 AAAAA 0 BRIGHT A_FireCustomMissile("ShotgunParticles2", random(-12,12), 0, 0, 0, 0, random(-9,9))
		TNT1 AAAAA 0 A_FireCustomMissile("DecorativeTracer", random(-3,3), 0, 0, 0, 0, random(-3,3))
		REVO A 0 A_Takeinventory("RevolverAmmo",1)
		REVO A 0 A_GiveInventory("SpentRound",1)
		REVO A 0 A_FireBullets (3.2, 3.3, 4, 13, "ShotgunPuff", FBF_NORANDOM)
		REVO A 0 A_FireBullets (3.2, 3.3, 5, 13, "ShotgunPuff2", FBF_NORANDOM)
		REVO A 0 A_JumpIfInventory("IsNOTTacticalClass", 1, 3)//Tactical Mode deals extra damage and recoil
		REVO A 0 A_FireBullets (3.2, 3.3, 5, 13, "ShotgunPuff2", FBF_NORANDOM)
		REVO A 0 A_SetPitch(pitch-0.9)
		REVO A 0 A_SetPitch(pitch-0.9)
		REVA C 1 BRIGHT
		TNT1 A 0 A_ZoomFactor(0.99)
		REVA D 1 BRIGHT
		TNT1 A 0 A_ZoomFactor(1.0)
		REVA E 1 A_SetPitch(pitch+0.3)
		REVA FG 1 A_SetPitch(pitch+0.3)
		REVA F 1
		REVA E 1
		REVA A 1 A_PlaySound("Revolver/Hammer", 4)
		REVA A 1 A_JumpIfInventory("FiredPrimary",1,"Fire")
		REVA A 0 A_Refire
		Goto Ready2

	AltFire://Fan The Hammer
		TNT1 A 0
		TNT1 A 0 A_JumpIfInventory("PistolBurstFire", 1, "StopAlt")
		TNT1 A 0 A_GiveInventory("PistolBurstFire", 1)
		TNT1 A 0 A_Print("Fan The Hammer")
		//Lower weapon
		REVO A 2 Offset(0, 45)//(+:Right | -:Left, +:Down | -:Up )
		REVO A 2 Offset(0, 55)
		REVO A 2 Offset(0, 65)
		REVO A 2 Offset(0, 75)
		Goto Ready2

	StopAlt:
		TNT1 A 0
		TNT1 A 0 A_TakeInventory("PistolBurstFire", 1)
		TNT1 A 0 A_Print("Single Action")
		//Raise weapon
		REVA A 2 Offset(0, 35)
		REVA A 2 Offset(0, 25)
		REVA A 2 Offset(0, 15)
		REVA A 2 Offset(0, 5)
		Goto Ready3

	StartSprint:
		REVO A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		SHTN A 0 A_Takeinventory("Zoomed",1)
        SHTN A 0 A_ZoomFactor(1.0)
		SHTN A 0 A_Takeinventory("ADSmode",1)
		RIFG A 0 A_SetCrosshair(0)
		SHTN A 0 A_Giveinventory("GoSpecial",1)
		TNT1 A 0 ACS_NamedExecuteAlways("BDSprint", 0, 0, 0, 0)//Makes player faster.
		Goto Sprinting

	Sprinting:
		REVP B 1 offset(-9,34) A_SetPitch(pitch -0.5)
		REVP B 1 offset(-6,36) A_SetPitch(pitch -0.5)
		REVP B 1 offset(-3,38) A_SetPitch(pitch -0.5)
		REVP B 1 offset(0,38) A_SetPitch(pitch -0.5)
		REVP B 1 offset(3,36) A_SetPitch(pitch -0.5)
		REVP B 1 offset(6,34) A_SetPitch(pitch -0.5)
		REVP B 1 offset(9,32) A_SetPitch(pitch -0.5)
		TNT1 A 0 A_GiveInventory("UsedStamina", 1)
		SMGG A 0 A_WeaponReady(WRF_NOBOB)
		SMGG A 0 A_JumpIfInventory("Kicking",1,"DoKick")
        SMGG A 0 A_JumpIfInventory("Taunting",1,"Taunt")
        SMGG A 0 A_JumpIfInventory("Reloading",1,"Reload")

		REVP B 1 offset(9,34) A_SetPitch(pitch +0.5)
		REVP B 1 offset(6,36) A_SetPitch(pitch +0.5)
		REVP B 1 offset(3,38) A_SetPitch(pitch +0.5)
		REVP B 1 offset(0,38) A_SetPitch(pitch +0.5)
		REVP B 1 offset(-3,36) A_SetPitch(pitch +0.5)
		REVP B 1 offset(-6,34) A_SetPitch(pitch +0.5)
		REVP B 1 offset(-9,32) A_SetPitch(pitch +0.5)
		TNT1 A 0 A_GiveInventory("UsedStamina", 1)
		SMGG A 0 A_WeaponReady(WRF_NOBOB)
		SMGG A 0 A_JumpIfInventory("Kicking",1,"DoKick")
        SMGG A 0 A_JumpIfInventory("Taunting",1,"Taunt")
        SMGG A 0 A_JumpIfInventory("Reloading",1,"Reload")

		PLAY A 0 ACS_NamedExecuteAlways("BDSprint", 0, 0, 0, 0)//Makes player faster.
		SMGG A 0 A_JumpIfInventory("IsRunning", 1, "Sprinting")
		Goto StopSprint

	StopSprint:
		REVP B 0 A_JumpIfInventory("PowerStrength", 1, 2)
		REVO A 0 A_JumpIfInventory("UsedStamina", 100, "StopSprintTired")
		REVP B 1 ACS_NamedExecuteAlways("BDTacticalSpeed", 0, 0, 0, 0)//Makes player slower.
		Goto Ready

	StopSprintTired:
		REVP B 1 ACS_NamedExecuteAlways("BDTacticalSpeed", 0, 0, 0, 0)//Makes player slower.
		REVO A 0 A_PlaySound("Tired", 2)
		Goto Ready

    Reload:
	    REVO A 0
		REVO A 0 A_TakeInventory("Reloading", 1)
		REVO A 0 A_JumpIfInventory("AmmoShell", 1, 2)
		Goto Ready
		REVO A 0
	    REVO A 0 A_Takeinventory("Zoomed",1)
        REVO A 0 A_ZoomFactor(1.0)
		REVO A 0 A_JumpIfInventory("RevolverAmmo",6,"Ready3")

	OpenCylinder:
        REVO A 0 A_JumpIfInventory("AmmoShell",1,1)//If there is no reserve ammo, don't reload, go back to Ready
        Goto Ready
		REVO A 0 A_JumpIfInventory("TurboReload", 1, "TurboReload")//Check if reloads are disabled.
		REVO A 0 A_JumpIfInventory("RevolverUnloaded",1,"OpenEmptyCylinder")
		REVR ABCDE 1 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
		A12R A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		A12R A 0 A_PLaySound("Revolver/Open")
		REVR FGH 1
		REVR I 8
		REVO A 0 A_JumpIfInventory("RevolverChangeAmmo", 1, "InsertBulletAnimation")

		REVR JK 1
		REVR K 8 Offset(0,64)
		Goto Dump

	OpenEmptyCylinder:
        REVO A 0
		REVR ABCDE 1 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
		A12R A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		A12R A 0 A_PLaySound("Revolver/Open")
		REVR PON 1
		REVR M 8
		Goto InsertBulletAnimation

	Dump:
		REVO A 0
		REVO A 0 A_JumpIfInventory("SpentRound",1,1)
        Goto PutMag
		TNT1 A 0 A_FireCustomMissile("ShotgunCasing",random(-120,-150),0,0,0,2,random(-30,-60))
        REVO A 0 A_TakeInventory("SpentRound",1)
		Loop

	PutMag:
		REVR K 8
		REVR LM 1

	InsertBulletAnimation:
		REVO A 0
		REVO A 0 A_JumpIfInventory("AmmoShell",1,1)//if there are no more reserve rounds, finish reload.
        Goto FinishedInsertingBullets
		REVO A 0 A_JumpIfInventory("RevolverAmmo",6,"FinishedInsertingBullets")
        REVO A 0 A_Giveinventory("RevolverAmmo",1)
		REVO A 0 A_Takeinventory("AmmoShell",1)
        TNT1 A 0 A_PlaySound("Shotgun/Reload")
		REVR M 4 Offset(0, 64)
		REVR M 4 Offset(0, 66)
        Loop

	FinishedInsertingBullets:
		REVO A 0
		TNT1 A 0 A_PLaySound("Revolver/Close")
		REVR IHGF 1
		REVR E 8
		REVR DCBA 1
		TNT1 A 0 A_PlaySound("Revolver/Hammer", 4)
		REVO KLMNOPQR 1
		TNT1 A 0 A_TakeInventory("RevolverChangeAmmo",1)
		TNT1 A 0 A_TakeInventory("RevolverUnloaded",1)
		Goto Ready

	 TurboReload:
		SHSS BC 0
	TurboBullets:
		REVO A 0
		REVO A 0 A_JumpIfInventory("RevolverAmmo",6,"FinishTurboReload")
        REVO A 0 A_JumpIfInventory("AmmoShell",1,3)
        Goto Ready3
        TNT1 AAAA 0
        REVO A 0 A_Giveinventory("RevolverAmmo",1)
		REVO A 0 A_Takeinventory("AmmoShell",1)
		Goto TurboBullets
	FinishTurboReload:
		TNT1 A 0 A_TakeInventory("RevolverUnloaded",1)
		REVO A 0 A_Refire
		Goto Ready3

	DualWield:
		TNT1 A 0
		TNT1 A 0 A_Takeinventory("StartDualWield",1)
		TNT1 A 0 A_GiveInventory("RevolverChangeAmmo", 1)
		REVO A 2 A_JumpIfInventory("AmmoMagnum",1,"Unload")
		Goto NoRounds

	NoRounds:
		TNT1 A 0 A_Print("You are out of magnum rounds.", 2)
		TNT1 A 0 A_TakeInventory("RevolverChangeAmmo",1)
		REVO A 5
		Goto Ready3

	Unload:
		REVO A 1
		REVO A 0 A_ZoomFactor(1.0)
		REVO A 0 A_Takeinventory("Unloading",1)
		REVO A 0 A_Takeinventory("ADSmode",1)
		REVO A 0 A_Takeinventory("Zoomed",1)
        REVO A 0 A_JumpIfInventory("RevolverAmmo",1,2)
		REVO A 0 A_JumpIfInventory("RevolverChangeAmmo",1,"FinishUnload")
        Goto Ready3
        TNT1 AAA 0
		REVR ABCDE 1 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
		A12R A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		A12R A 0 A_PLaySound("Revolver/Open")
		REVR FGH 1
		REVR I 8
		REVR JK 1
		A12R A 0 A_PLaySound("Revolver/Unload")
		REVO A 0 A_GiveInventory("RevolverUnloaded",1)
		REVR K 8 Offset(0,64)

	RemoveBullets:
		TNT1 AAAA 0
		REVO A 0 A_JumpIfInventory("RevolverAmmo",1,3)
		Goto FinishUnload
        TNT1 AAAAAA 0
		REVO A 0 A_Takeinventory("RevolverAmmo",1)
		REVO A 0 A_Giveinventory("AmmoShell",1)
		Loop

	FInishUnload:
		TNT1 A 0
		REVR K 8
		REVR LM 1
		REVR M 4
		TNT1 A 0 A_PLaySound("Revolver/Close")
		REVR NOP 1
		REVR E 8
		REVR DCBA 1
		REVO A 0 A_TakeInventory("SpentRound", 6)
		REVO A 0 A_Takeinventory("Unloading",1)
		TNT1 A 0 A_JumpIfInventory("RevolverChangeAmmo", 1, "ChangeAmmo")
		Goto Ready3

	ChangeAmmo:
		TNT1 A 0
		TNT1 A 0 A_GiveInventory("Revolver", 1)
		TNT1 A 0 A_GiveInventory("Reloading",1)
		TNT1 A 0 A_SelectWeapon("Revolver")
		Goto Ready3

	NoAmmo:
		RIFG A 0
		RIFG A 0 A_ZoomFactor(1.0)
		RIFG A 0 A_Takeinventory("Zoomed",1)
		RIFG A 0 A_Takeinventory("ADSmode",1)
		RIFG A 0 A_JumpIfInventory("TurboReload",1,"TurboReload")
		TNT1 A 0 A_PlaySound("weapons/empty", 4)
	NoAmmo2:
		RIFG A 0 A_JumpIfInventory("TossGrenade",1,"TossGrenade")
	    RIFG A 0 A_JumpIfInventory("Taunting",1,"Taunt")
		RIFG A 0 A_JumpIfInventory("Salute1", 1, "Salute")
		RIFG A 0 A_JumpIfInventory("Salute2", 1, "Salute")
		RIFG A 0 A_JumpIfInventory("ExecuteDownedEnemy", 1, "DoExecution")
		RIFG A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		REVO A 0 A_JumpIfInventory("PistolBurstFire",1,"NoAmmo3")
		REVO A 5 A_WeaponReady(WRF_ALLOWRELOAD)
		RIFG A 0 A_JumpIfInventory("Reloading",1,"Reload")
		RIFG A 0 A_JumpIfInventory("TurboReload",1,"TurboReload")
		TNT1 A 0 A_JumpIfInventory("NoAutoReload", 1, 2)
		RIFG A 0 A_JumpIfInventory("AmmoMagnum",1,"Reload")
		RIFG A 0 A_JumpIfInventory("IsRunning",1,"StartSprint")
		Goto Ready3
	NoAmmo3:
		REVA A 5 A_WeaponReady(WRF_ALLOWRELOAD)
		RIFG A 0 A_JumpIfInventory("Reloading",1,"Reload")
		RIFG A 0 A_JumpIfInventory("TurboReload",1,"TurboReload")
		TNT1 A 0 A_JumpIfInventory("NoAutoReload", 1, 2)
		RIFG A 0 A_JumpIfInventory("AmmoMagnum",1,"Reload")
		RIFG A 0 A_JumpIfInventory("IsRunning",1,"StartSprint")
		Goto Ready2
	}
}