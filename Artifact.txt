// Invulnerability Sphere ---------------------------------------------------

ACTOR InvulnerabilitySphere_  : PowerupGiver Replaces InvulnerabilitySphere
{
	Game Doom
	SpawnID 133
	+COUNTITEM
	+INVENTORY.AUTOACTIVATE
	+INVENTORY.ALWAYSPICKUP
	+INVENTORY.BIGPOWERUP
	Inventory.MaxAmount 0
	Powerup.Type Invulnerable
	//Powerup.Color White //InverseMap
	Powerup.Colormap 0.0, 0.0, 0.0, 1.0,1.0,1.0
	Inventory.PickupMessage "$GOTINVUL"
	Inventory.PickupSound "misc/p_pkup"
	Tag "Invulnerability"
	States
	{
	Spawn:
		TNT1 A 0 A_SpawnItem("GreenFlareMediumItem",0,20)
		PINV ABCD 4 Bright
		Loop
	}
}

// Soulsphere --------------------------------------------------------------

ACTOR LifeSphere : CustomInventory Replaces SoulSphere
{
	SpawnID 25
	+COUNTITEM
	+INVENTORY.ALWAYSPICKUP
	Inventory.PickupMessage "Soulsphere"
	Inventory.PickupSound "misc/p_pkup"
	Tag "Soulsphere"
	States
	{
	Spawn:
		TNT1 A 0 A_SpawnItem("BlueFlareMediumItem",0,20)
		SOUL ABCD 4 Bright
		Loop
	Pickup:
        TNT1 AAAA 0 A_CustomMissile ("ShinnySmoke", 0, 0, random (0, 360), 2, random (0, 360))
		TNT1 A 0 A_GiveInventory("SoulSphereHealth", 1)
		Stop
	}
}

ACTOR SoulSphereHealth : Health
{
	Inventory.Amount 100
	Inventory.MaxAmount 200
	+INVENTORY.ALWAYSPICKUP
}

ACTOR EnergySphere : CustomInventory Replaces Megasphere
{
	SpawnID 132
	+COUNTITEM
	+INVENTORY.ALWAYSPICKUP
	Inventory.PickupMessage "Megasphere"
	Inventory.PickupSound "misc/p_pkup"
	Tag "Megasphere"
	States
	{
	Spawn:
		TNT1 A 0 A_SpawnItem("WhiteFlareMediumItem",0,20)
		MEGA ABCD 4 BRIGHT
		Loop
	Pickup:
        TNT1 AAAA 0 A_CustomMissile ("ShinnySmoke", 0, 0, random (0, 360), 2, random (0, 360))
		TNT1 A 0 A_GiveInventory("HeavyArmor", 1)
		TNT1 AA 0 A_GiveInventory("SoulsphereHealth", 1)
		Stop
	}
}

// infrared -----------------------------------------------------------------

ACTOR NightVision : CustomInventory Replaces InfraRed
{
	Game Doom
	SpawnID 138
	+COUNTITEM
	+INVENTORY.ALWAYSPICKUP
	Inventory.PickupMessage "You got the Infrared Googles."
	Inventory.PickupSound "misc/p_pkup"
	States
	{
	Spawn:
		TNT1 A 0 A_SpawnItem("WhiteFlareSmallItem",0,0)
		SVIS ABCDE 3 BRIGHT
		Loop

	Pickup:
		TNT1 A 1
		TNT1 A 0 A_GiveInventory("HasInfrared", 1)
		TNT1 A 0 A_GiveInventory("NightVisionGiver", 1)
		TNT1 A 0 ACS_NamedExecuteAlways("BDTakeInfraRed", 0, 0, 0, 0)
		TNT1 A 1
		Stop
	}
}

ACTOR NightVisionGiver : PowerupGiver
{
	Game Doom
	+COUNTITEM
	+INVENTORY.AUTOACTIVATE
	+INVENTORY.ALWAYSPICKUP
	Inventory.MaxAmount 0
	Powerup.Type Torch
	Powerup.Color 99 99 99
	Inventory.PickupMessage "$GOTVISOR"
	Powerup.Duration -120
	States
	{
	Spawn:
		TNT1 A 0 A_SpawnItem("WhiteFlareSmallItem",0,5)
		SVIS ABCDE 3 BRIGHT
		Loop
	}
}

ACTOR VehicleInfraredGiver : PowerupGiver
{
	Game Doom
	+COUNTITEM
	+INVENTORY.AUTOACTIVATE
	+INVENTORY.ALWAYSPICKUP
	Inventory.MaxAmount 0
	Powerup.Type PowerLightAmp
	Powerup.Color 99 99 99
	Inventory.PickupMessage "$GOTVISOR"
	Powerup.Duration 0x7FFFFFFD
	States
	{
	Spawn:
		TNT1 A 0 A_SpawnItem("WhiteFlareSmallItem",0,5)
		SVIS ABCDE 3 BRIGHT
		Loop
	}
}

// Berserk ------------------------------------------------------------------

ACTOR BerserkPack : CustomInventory Replaces Berserk
{
	Game Doom
    Scale 1.2
	SpawnID 134
	+COUNTITEM
	+INVENTORY.ALWAYSPICKUP
	Inventory.PickupMessage "$GOTBERSERK"
    Inventory.PickupSound "BERSPKUP"
	Tag "Berserk Pack"
	States
	{
	Spawn:
		MEDK C 0
		MEDK C 0 A_SpawnItemEx("EvidenceCheckerbERSERk", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
	Spawn1:
		MEDK C 16 BRIGHT A_SpawnItem("RedFlareMediumItem",0,10)
		Loop
	Pickup:
		TNT1 A 0 A_GiveInventory("PowerStrength")
		TNT1 A 0 HealThing(100, 0)
		TNT1 A 20
		TNT1 A 0 A_Print ("Press Reload (R) to alternate execution modes")
		Stop
	}
}

ACTOR SuperBlurSphere : CustomInventory 2054
{
  +COUNTITEM
  +VISIBILITYPULSE
  +INVENTORY.AUTOACTIVATE
  +INVENTORY.ALWAYSPICKUP
  +INVENTORY.BIGPOWERUP
  RenderStyle Translucent
  Inventory.PickupMessage "$GOTINVIS" // "Partial Invisibility"
  //$Category BD Power-Ups
  //$Icon "powerup"
  //$Colour rgb(220, 100, 220)
  //$Sprite PINSA0
  //$NotAngled
  States
  {
  Spawn:
	TNT1 A 0 A_SpawnItem("RedFlareMediumItem", 0, 20)
     PINS ABCD 4 Bright
     Loop
  Pickup:
	 TNT1 A 1
     TNT1 A 0 A_GiveInventory("IsInvisible", 1)
	 TNT1 A 0 A_GiveInventory("Blursphere", 1)
	 TNT1 A 0 ACS_NamedExecuteAlways("BDTakeInvisibility", 0, 0, 0, 0)
	 TNT1 A 1
	 Stop
  }
}

ACTOR DemonStrengthRune : BerserkPack 2053
{
	Game Doom
    Scale 0.6
	+COUNTITEM
	+INVENTORY.ALWAYSPICKUP
	Inventory.PickupMessage "You got the Demon Strength Rune. RIP AND TEAR!"
	Inventory.PickupSound "misc/p_pkup"
	//$Category BD Power-Ups
	//$Icon "powerup"
	//$Colour rgb(220, 100, 220)
	//$Sprite DPRFA0
	//$NotAngled
	States
	{
	Spawn:
		TNT1 A 0 A_SpawnItem("RedFlareMediumItem",0,20)
		DPRF ABCB 4 BRIGHT
		Loop
	Pickup:
		TNT1 A 0 A_GiveInventory("PowerStrength", 1)
		TNT1 A 0 HealThing(10, 0)
		TNT1 A 20
		TNT1 A 0 A_Print ("You got the Demon Strength Rune. RIP AND TEAR!")
		Stop
	}
}

ACTOR RadSuits : CustomInventory Replaces Radsuit
{
  Height 46
  SpawnID 136
  +INVENTORY.AUTOACTIVATE
  +INVENTORY.ALWAYSPICKUP
  Inventory.MaxAmount 0
  Inventory.PickupMessage "$GOTSUIT" // "Radiation Shielding Suit"
  States
  {
  Spawn:
    SUIT A 10
	SUIT A 10 BRIGHT
	Loop
  Pickup:
		TNT1 A 0
		TNT1 A 0 A_GiveInventory("Radsuit3", 1)
		TNT1 A 0 A_GiveInventory("Radsuit2", 1)
		Stop
  }
}

ACTOR RadSuit2 : PowerupGiver
{
  Height 46
  +INVENTORY.AUTOACTIVATE
  +INVENTORY.ALWAYSPICKUP
  Inventory.MaxAmount 0
  Inventory.PickupMessage "$GOTSUIT" // "Radiation Shielding Suit"
  Powerup.Type "Mask"
  Powerup.Duration -60
  States
  {
  Spawn:
    SUIT A 10
	SUIT A 10 BRIGHT
	Loop
  }
}

ACTOR RadSuit3 : PowerupGiver
{
  Height 46
  +INVENTORY.AUTOACTIVATE
  +INVENTORY.ALWAYSPICKUP
  Inventory.MaxAmount 0

  Inventory.PickupMessage "$GOTSUIT" // "Radiation Shielding Suit"
  Powerup.Type "IronFeet"
  States
  {
  Spawn:
    SUIT A 10
	SUIT A 10 BRIGHT
	Loop
  }
}

//A randomizer for Demon Runes
ACTOR DemonRuneMix: CustomInventory 2063
{
	Game Doom
	scale 1.0
	radius 16
	height 16
	+INVENTORY.BIGPOWERUP
	damage 0
	Mass 1
	Inventory.PickupMessage "You got the Demon Sphere"
	Inventory.PickupSound "misc/p_pkup"
	//$Category BD Power-Ups
	//$Icon "powerup"
	//$Colour rgb(220, 100, 220)
	//$Sprite DMRUA0
	//$NotAngled
    States
    {
	 Spawn:
		TNT1 A 0
		TNT1 A 0 ACS_NamedExecuteAlways("DemonRuneCheck")
		TNT1 A 4
	Live:
		TNT1 A 0 A_SpawnItem("RedFlareMediumItem",0,20)
		DMRU ABCD 4 BRIGHT
		Loop
	Pickup:
	    TNT1 A 1
		TNT1 A 0 A_TakeInventory("PowerInvulnerable", 1)
		TNT1 A 0 A_Jump(128, "Pickup2")
		TNT1 A 0 ACS_NamedExecute("BaronMorph")
		Stop
	Pickup2:
		TNT1 A 1
	    TNT1 A 0 ACS_NamedExecute("RevenantMorph")
		Stop
	Cancels:
		TNT1 A 1
		TNT1 A 0 A_SpawnItem("DemonStrengthRune")
		Stop
    }
}

/* --- UNMAKER UPGRADES ----------------------------------------------------- */
ACTOR UnmakerUpgrade1Icon : Inventory
{
	Game Doom
	+UNDROPPABLE
	Inventory.MaxAmount 1
}

ACTOR UnmakerUpgrade2Icon : Inventory
{
	Game Doom
	+UNDROPPABLE
	Inventory.MaxAmount 1
}

ACTOR UnmakerUpgrade3Icon : Inventory
{
	Game Doom
	+UNDROPPABLE
	Inventory.MaxAmount 1
}

ACTOR UnmakerUpgrades : Inventory
{
	Game Doom
	+UNDROPPABLE
	Inventory.MaxAmount 3
}

ACTOR 64UnmakerUpgradeSpawner : RandomSpawner
{
	DropItem "64UnmakerUpgrade1"
	DropItem "64UnmakerUpgrade2"
	DropItem "64UnmakerUpgrade3"
}

ACTOR 64UnmakerUpgrade1 : CustomInventory 1042
{
	//Orange Artifact (First Upgrade)
	//$Color 19
	//$Category Doom 64 Retribution
	//$Icon "powerup"
	//$Colour rgb(220,100,220)
	//$Sprite ART1A0
	//$NotAngled
	Game Doom
	Scale 0.6
	Inventory.Amount 1
	Inventory.MaxAmount 1
	Inventory.InterHubAmount 1
	Inventory.Icon "ART1A0"
	Inventory.PickupMessage "$GOTARTIFACT1"
	Inventory.PickupSound "misc/p_pkup"
	+DONTGIB
	+COUNTITEM
	-INTERHUBSTRIP
	States
	{
		Spawn:
			TNT1 A 0 A_SpawnItem("YellowFlareMediumItem",0,20)
			ART1 ABCDEDCB 2 BRIGHT
			Loop
		Pickup:
			TNT1 A 0
			TNT1 A 0 A_JumpIfInventory("IsPlayingAsPurist",1,1)
			Goto Berserk
			TNT1 A 0 A_JumpIfInventory("UnmakerUpgrade1Icon", 1, "AlreadyTaken")
			TNT1 A 0 A_GiveInventory("UnmakerUpgrades", 1)
			TNT1 A 0 A_GiveInventory("UnmakerUpgrade1Icon", 1)
			Stop
		Berserk:
			TNT1 A 0 A_GiveInventory("PowerStrength", 1)
			TNT1 A 0 HealThing(100, 0)
			TNT1 A 20
			TNT1 A 0 A_Print ("You got the Demon Strength Rune. RIP AND TEAR!")
			Stop
		AlreadyTaken:
			TNT1 A 0
			Fail
	}
}

ACTOR 64UnmakerUpgrade2 : CustomInventory 1043
{
	// Purple Artifact (Second Upgrade)
	//$Color 5
	//$Category Doom 64 Retribution
	//$Icon "powerup"
	//$Colour rgb(220,100,220)
	//$Sprite ART2A0
	//$NotAngled
	Game Doom
	Scale 0.6
	Inventory.Amount 1
	Inventory.MaxAmount 1
	Inventory.InterHubAmount 1
	Inventory.Icon "ART2A0"
	Inventory.PickupMessage "$GOTARTIFACT2"
	Inventory.PickupSound "misc/p_pkup"
	+DONTGIB
	+COUNTITEM
	-INTERHUBSTRIP
	States
	{
		Spawn:
			TNT1 A 0 A_SpawnItem("VioletFlareMediumItem",0,20)
			ART2 ABCDEDCB 2 BRIGHT
			Loop
		Pickup:
			TNT1 A 0
			TNT1 A 0 A_JumpIfInventory("IsPlayingAsPurist",1,1)
			Goto Berserk
			TNT1 A 0 A_JumpIfInventory("UnmakerUpgrade2Icon", 1, "AlreadyTaken")
			TNT1 A 0 A_GiveInventory("UnmakerUpgrades", 1)
			TNT1 A 0 A_GiveInventory("UnmakerUpgrade2Icon", 1)
			Stop
		Berserk:
			TNT1 A 0 A_GiveInventory("PowerStrength", 1)
			TNT1 A 0 HealThing(100, 0)
			TNT1 A 20
			TNT1 A 0 A_Print ("You got the Demon Strength Rune. RIP AND TEAR!")
			Stop
		AlreadyTaken:
			TNT1 A 0
			Fail
	}
}

ACTOR 64UnmakerUpgrade3 : CustomInventory 1044
{
	// Cyan Artifact (Third Upgrade)
	//$Color 3
	//$Category Doom 64 Retribution
	//$Icon "powerup"
	//$Colour rgb(220,100,220)
	//$Sprite ART3A0
	//$NotAngled
	Game Doom
	Scale 0.6
	Inventory.Amount 1
	Inventory.MaxAmount 1
	Inventory.InterHubAmount 1
	Inventory.Icon "ART3A0"
	Inventory.PickupMessage "$GOTARTIFACT3"
	Inventory.PickupSound "misc/p_pkup"
	+DONTGIB
	+COUNTITEM
	-INTERHUBSTRIP
	States
	{
		Spawn:
			TNT1 A 0 A_SpawnItem("BlueFlareMediumItem",0,20)
			ART3 ABCDEDCB 2 BRIGHT
			Loop
		Pickup:
			TNT1 A 0
			TNT1 A 0 A_JumpIfInventory("IsPlayingAsPurist",1,1)
			Goto Berserk
			TNT1 A 0 A_JumpIfInventory("UnmakerUpgrade3Icon", 1, "AlreadyTaken")
			TNT1 A 0 A_GiveInventory("UnmakerUpgrades", 1)
			TNT1 A 0 A_GiveInventory("UnmakerUpgrade3Icon", 1)
			Stop
		Berserk:
			TNT1 A 0 A_GiveInventory("PowerStrength", 1)
			TNT1 A 0 HealThing(100, 0)
			TNT1 A 20
			TNT1 A 0 A_Print ("You got the Demon Strength Rune. RIP AND TEAR!")
			Stop
		AlreadyTaken:
			TNT1 A 0
			Fail
	}
}