ACTOR Rocket_Launcher : BrutalWeapon
{
	Game Doom
	SpawnID 29
	Speed 20
	Damage 20
	Weapon.SelectionOrder 2500
	Weapon.AmmoUse1 0
	Weapon.AmmoUse2 0
	Weapon.AmmoGive2 0
	Weapon.AmmoGive1 1
	Weapon.AmmoType1 "AmmoRocket"
	Weapon.AmmoType2 "RocketRounds"
    Inventory.PickupSound "Rocket/Draw"
	+FORCEXYBILLBOARD
	+WEAPON.NOAUTOAIM
	+WEAPON.NOAUTOFIRE
	+WEAPON.EXPLOSIVE
	+WEAPON.NOALERT
	Inventory.PickupMessage "$GOTLAUNCHER"
	Tag "Rocket Launcher"
	Scale 0.8
	Dropitem "RocketLauncherSpawner"
	States
	{
	KickFlash://18 tics
		MISS EFG 1
		MISS H 15
		MISS GFE 1
		Stop

	SlideFlashEnd://10 tics
		MISS H 10
		MISS GFE 1
		Stop

	Deselect:
        RLNG A 0
		RLNG A 0 A_Takeinventory("Zoomed",1)
		RLNG A 0 A_Takeinventory("PowerLightAmp", 1)
        RLNG A 0 A_ZoomFactor(1.0)
		RLNG A 0 A_TakeInventory("TossGrenade", 1)
		TNT1 A 0 A_TakeInventory("StartDualWield",1)
		RLNG A 0 A_SetCrosshair(0)
		MISS EFGH 1
		TNT1 AAAAAAAAAAAAAAAAAA 0 A_Lower
		TNT1 A 1 A_Lower
		Wait

	Select:
		RLNG A 0 A_Giveinventory("GoSpecial",1)
		TNT1 A 0 A_TakeInventory("FistsSelected", 1)
		TNT1 A 0 A_TakeInventory("ShotgunSelected", 1)
		TNT1 A 0 A_TakeInventory("MinigunSelected", 1)
		TNT1 A 0 A_TakeInventory("SSGSelected", 1)
		TNT1 A 0 A_GiveInventory("RocketLauncherSelected", 1)
		TNT1 A 0 A_TakeInventory("GrenadeLauncherSelected", 1)
		TNT1 A 0 A_TakeInventory("PlasmaGunSelected", 1)
		TNT1 A 0 A_TakeInventory("RailGunSelected", 1)
		TNT1 A 0 A_TakeInventory("RevenantLauncherSelected", 1)
		TNT1 A 0 A_TakeInventory("SubMachineGunSelected", 1)
		TNT1 A 0 A_TakeInventory("BFG10KSelected", 1)
		TNT1 A 0 A_TakeInventory("BFGSelected", 1)
		TNT1 A 0 A_TakeInventory("FlameCannonSelected", 1)
		TNT1 A 0 A_TakeInventory("SawSelected", 1)
		RLNG A 0 A_Takeinventory("HasBarrel",1)
		RLNG A 0 A_TakeInventory("TossGrenade", 1)
		RLNG A 0 A_GunFlash
		TNT1 A 1 A_Raise
		TNT1 AAAAAAAAAAAA 0 A_Raise
		Goto SelectAnimation

	SelectAnimation:
		TNT1 A 0
		MISS DCBA 1 A_JumpIfInventory("GoFatality", 1, "Steady")
		RLNG A 0 A_PlaySound("Rocket/Draw")
		RLNG A 0 A_JumpIfInventory("Reloading",1,"Reload")
		Goto Ready

	Ready3:
	Ready:
		RLNG A 0 A_JumpIfInventory("RLUnloaded", 1, "ReadyNoAmmo")
	    RLNG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		RLNG A 0 A_JumpIfInventory("Unloading",1,"Unload")
        RLNG A 0 A_JumpIfInventory("Reloading",1,"Reload")
        RLNG A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		RLNG A 0 A_JumpIfInventory("Salute1", 1, "Salute")
		RLNG A 0 A_JumpIfInventory("Salute2", 1, "Salute")
        RLNG A 0 A_JumpIfInventory("Taunting",1,"GoTaunt")
		RLNG A 0 A_JumpIfInventory("TossGrenade",1,"TossGrenade")
		TNT1 A 0 A_JumpIfInventory("ExecuteDownedEnemy", 1, "DoExecution")
		RLNG A 0 A_JumpIfInventory("IsRunning",1,"StartSprint")
		RLNG A 0 A_JumpIfInventory("Zoomed", 1, "Ready2")
		RLNG A 0 A_JumpIfInventory("FiredPrimary",1,"Fire")
		Loop

	Ready2:
		TNT1 A 0
		SNIP A 1 BRIGHT A_WeaponReady(WRF_ALLOWRELOAD)
		RLNG A 0 A_JumpIfInventory("FiredPrimary",1,"Fire")
		RLNG A 0 A_JumpIfInventory("Unloading",1,"Unload")
		RLNG A 0 A_JumpIfInventory("Kicking",1,"DoKick")
        RLNG A 0 A_JumpIfInventory("Taunting",1,"GoTaunt")
		RLNG A 0 A_JumpIfInventory("Salute1", 1, "Salute")
		RLNG A 0 A_JumpIfInventory("Salute2", 1, "Salute")
		RLNG A 0 A_JumpIfInventory("Reloading",1,"Reload")
		RLNG A 0 A_JumpIfInventory("IsRunning",1,"StartSprint")
		RLNG A 0 A_JumpIfInventory("TossGrenade",1,"TossGrenade")
		TNT1 A 0 A_JumpIfInventory("ExecuteDownedEnemy", 1, "DoExecution")
		Loop

	ReadyNoAmmo:
	    MISR J 1 A_WeaponReady(WRF_ALLOWRELOAD)
		RLNG A 0 A_JumpIfInventory("Unloading",1,"Unload")
        RLNG A 0 A_JumpIfInventory("Reloading",1,"Reload")
        RLNG A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		RLNG A 0 A_JumpIfInventory("Salute1", 1, "Salute")
		RLNG A 0 A_JumpIfInventory("Salute2", 1, "Salute")
        RLNG A 0 A_JumpIfInventory("Taunting",1,"GoTaunt")
		RLNG A 0 A_JumpIfInventory("TossGrenade",1,"TossGrenade")
		RLNG A 0 A_JumpIfInventory("IsRunning",1,"StartSprint")
		Loop

	NoAmmo:
		RLNG A 0
		RLNG A 0 A_Takeinventory("PowerLightAmp", 1)
		RLNG A 0 A_ZoomFactor(1.0)
		RLNG A 0 A_Takeinventory("Zoomed",1)
		RLNG A 0 A_Takeinventory("ADSmode",1)
		RLNG A 0 A_SetCrosshair(0)
		RLNG A 0 A_JumpIfInventory("TurboReload",1,"InsertBullets")
		TNT1 A 0 A_PlaySound("weapons/empty", 4)
		RLNG A 0 A_JumpIfInventory("RLUnloaded", 1, "NoAmmo3")
	NoAmmo2:
		RLNG A 0 A_JumpIfInventory("TossGrenade",1,"TossGrenade")
	    RLNG A 0 A_JumpIfInventory("Taunting",1,"GoTaunt")
		RLNG A 0 A_JumpIfInventory("Salute1", 1, "Salute")
		RLNG A 0 A_JumpIfInventory("Salute2", 1, "Salute")
		RLNG A 0 A_JumpIfInventory("ExecuteDownedEnemy", 1, "DoExecution")
		RLNG A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		RLNG A 5 A_WeaponReady(WRF_ALLOWRELOAD)
		RLNG A 0 A_JumpIfInventory("Reloading",1,"Reload")
		RLNG A 0 A_JumpIfInventory("TurboReload",1,"InsertBullets")
		TNT1 A 0 A_JumpIfInventory("NoAutoReload", 1, 2)
		RLNG A 0 A_JumpIfInventory("AmmoRocket",1,"Reload")
		RLNG A 0 A_JumpIfInventory("IsRunning",1,"StartSprint")
		Goto Ready3
	NoAmmo3:
		RLNG A 0 A_JumpIfInventory("TossGrenade",1,"TossGrenade")
	    RLNG A 0 A_JumpIfInventory("Taunting",1,"GoTaunt")
		RLNG A 0 A_JumpIfInventory("Salute1", 1, "Salute")
		RLNG A 0 A_JumpIfInventory("Salute2", 1, "Salute")
		RLNG A 0 A_JumpIfInventory("ExecuteDownedEnemy", 1, "DoExecution")
		RLNG A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		MISR J 5 A_WeaponReady(WRF_ALLOWRELOAD)
		RLNG A 0 A_JumpIfInventory("Reloading",1,"Reload")
		RLNG A 0 A_JumpIfInventory("TurboReload",1,"InsertBullets")
		TNT1 A 0 A_JumpIfInventory("NoAutoReload", 1, 2)
		RLNG A 0 A_JumpIfInventory("AmmoRocket",1,"Reload")
		RLNG A 0 A_JumpIfInventory("IsRunning",1,"StartSprint")
		Goto ReadyNoAmmo

	StartSprint:
		MISS EFG 1 A_WeaponReady(WRF_ALLOWRELOAD)
		RLNG A 0 A_Takeinventory("Zoomed",1)
		RLNG A 0 A_Takeinventory("PowerLightAmp", 1)
        RLNG A 0 A_ZoomFactor(1.0)
		RLNG A 0 A_Takeinventory("ADSmode",1)
		RLNG A 0 A_SetCrosshair(0)
		RLNG A 0 A_Giveinventory("GoSpecial",1)
		TNT1 A 0 ACS_NamedExecuteAlways("BDSprint", 0, 0, 0, 0)//Makes player faster.

	Sprinting:
		MISS H 1 offset(-9,34) A_SetPitch(pitch -0.5)
		MISS H 1 offset(-6,36) A_SetPitch(pitch -0.5)
		MISS H 1 offset(-3,38) A_SetPitch(pitch -0.5)
		MISS H 1 offset(0,38) A_SetPitch(pitch -0.5)
		MISS H 1 offset(3,36) A_SetPitch(pitch -0.5)
		MISS H 1 offset(6,34) A_SetPitch(pitch -0.5)
		MISS H 1 offset(9,32) A_SetPitch(pitch -0.5)
		TNT1 A 0 A_GiveInventory("UsedStamina", 2)
		RLNG A 0 A_WeaponReady(WRF_NOBOB)
		RLNG A 0 A_JumpIfInventory("Kicking",1,"DoKick")
        RLNG A 0 A_JumpIfInventory("Taunting",1,"Taunt")
        RLNG A 0 A_JumpIfInventory("Reloading",1,"Reload")

		MISS H 1 offset(9,34) A_SetPitch(pitch +0.5)
		MISS H 1 offset(6,36) A_SetPitch(pitch +0.5)
		MISS H 1 offset(3,38) A_SetPitch(pitch +0.5)
		MISS H 1 offset(0,38) A_SetPitch(pitch +0.5)
		MISS H 1 offset(-3,36) A_SetPitch(pitch +0.5)
		MISS H 1 offset(-6,34) A_SetPitch(pitch +0.5)
		MISS H 1 offset(-9,32) A_SetPitch(pitch +0.5)
		TNT1 A 0 A_GiveInventory("UsedStamina", 2)
		RLNG A 0 A_WeaponReady(WRF_NOBOB)
		RLNG A 0 A_JumpIfInventory("Kicking",1,"DoKick")
        RLNG A 0 A_JumpIfInventory("Taunting",1,"Taunt")
        RLNG A 0 A_JumpIfInventory("Reloading",1,"Reload")

		PLAY A 0 ACS_NamedExecuteAlways("BDSprint", 0, 0, 0, 0)//Makes player faster.
		RLNG A 0 A_JumpIfInventory("IsRunning", 1, "Sprinting")
		Goto StopSprint

	StopSprint:
		MISS GFE 1
		MISP A 0 A_JumpIfInventory("PowerStrength", 1, 2)
		RLNG A 0 A_JumpIfInventory("UsedStamina", 100, "StopSprintTired")
		MISP A 0 ACS_NamedExecuteAlways("BDTacticalSpeed", 0, 0, 0, 0)//Makes player slower.
		Goto Ready

	StopSprintTired:
		MISP A 0 ACS_NamedExecuteAlways("BDTacticalSpeed", 0, 0, 0, 0)//Makes player slower.
		RLNG A 0 A_PlaySound("Tired", 2)
		Goto Ready

	GoTaunt:
		RLNG A 0
		RLNG A 0 A_Light0
		RLNG A 0 A_Takeinventory("Zoomed",1)
		RLNG A 0 A_Takeinventory("PowerLightAmp", 1)
        RLNG A 0 A_ZoomFactor(1.0)
		RLNG A 0 A_SetCrosshair(0)
		Goto Taunt

	Fire:
		RLNG A 0
        RLNG A 0 A_JumpIfInventory("RocketRounds",1,1)
        Goto NoAmmo
        TNT1 AAAA 0
		RLNG A 0 A_ALertMonsters
		RLNG A 0 A_JumpIfInventory("Zoomed",1,"ZoomedFire")
		RLNF A 1 BRIGHT
		TNT1 A 0 A_ZoomFactor(0.98)
        RLNG A 0 A_PlaySound("Rocket/Fire")
		RIFF A 0 A_FireCustomMissile("DistantFireSoundRocket", random(-1,1), 0, 0, -12, 0, random(-1,1))
		RLNF B 1 BRIGHT
		RLNG A 0 A_SpawnItemEx("PlayerMuzzle2",32,0,28)
		RLNG A 0 A_FireCustomMissile("Rocket2", 0, 1, 0, 0)
		RLNG A 0 A_FireCustomMissile("Alerter",0,0)
		Goto RecoilAnimation

	RecoilAnimation:
		RLNG A 0 A_Takeinventory("RocketRounds",1)
		RLNF C 1
		TNT1 A 0 A_ZoomFactor(1.0)
		RLNF DEFGH 1 A_WeaponReady(WRF_NOFIRE | WRF_NOBOB | WRF_ALLOWRELOAD)
		RLNG ABCDA 2 A_WeaponReady(WRF_NOFIRE| WRF_NOBOB | WRF_ALLOWRELOAD)//Allows quick switch
        RLNG A 0 A_JumpIfInventory("FiredPrimary",1,"Fire")
        Goto Ready

	ZoomedFire:
		SNIP A 1 BRIGHT
		TNT1 A 0 A_ZoomFactor(2.99)
        RLNG A 0 A_PlaySound ("Rocket/Fire")
		RIFF A 0 A_FireCustomMissile("DistantFireSoundRocket", random(-1,1), 0, 0, -12, 0, random(-1,1))
		SNIP A 1 BRIGHT
		RLNG A 0 A_SpawnItemEx("PlayerMuzzle2",32,0,28)
		RLNG A 0 A_FireCustomMissile("Rocket2", 0, 1, 0, 0)
		RLNG A 0 A_FireCustomMissile("Alerter",0,0)
		Goto ZoomedRecoil

	ZoomedRecoil:
		RLNG A 0 A_Takeinventory("RocketRounds",1)
		SNIP A 1 BRIGHT
		TNT1 A 0 A_ZoomFactor(3.0)
		SNIP A 15 BRIGHT A_WeaponReady(WRF_NOFIRE | WRF_NOBOB | WRF_ALLOWRELOAD)
		RLNG A 0 A_JumpIfInventory("FiredSecondary",1,"HoldAim")//If still pressing alt fire, it means its holding aim
        RLNG A 0 A_JumpIfInventory("FiredPrimary",1,"Fire")
        Goto Ready2

    Reload:
		"####" "#" 1
		RLNG A 0 A_Takeinventory("Reloading",1)
		RLNG A 0 A_ZoomFactor(1.0)
		RLNG A 0 A_TakeInventory("Zoomed", 1)
		RLNG A 0 A_Takeinventory("PowerLightAmp", 1)
		RLNG A 0 A_SetCrosshair(0)
        RLNG A 0 A_JumpIfInventory("RocketRounds",6,"Ready")
        RLNG A 0 A_JumpIfInventory("AmmoRocket",1,1)
        Goto Ready
        TNT1 AAA 0
		RLNG A 0 A_JumpIfInventory("RLUnloaded",1,"PutMag")
		RLNG A 0 A_JumpIfInventory("TurboReload",1,"InsertBullets")
        MISR ABCDEF 1 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
		A12R A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		MISR G 4 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
		A12R A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		RLNG A 0 A_PlaySound("rocket/reload1")
		A12G A 0 A_GiveInventory("RLUnloaded",1)
        MISR HI 1
		MISR J 8
        RLNG A 0 A_JumpIfInventory("RocketRounds",1,"PutMag")//if drum is not empty then retain it
        MISG A 0 A_FireCustomMissile("RocketCaseSpawn",-20,0,2,-10)

	PutMag:
		MISR J 8
		A12G A 0 A_Takeinventory("Reloading",1)
		A12G A 0 A_Takeinventory("ADSmode",1)
		A12G A 0 A_Takeinventory("Zoomed",1)
		RLNG A 0 A_Takeinventory("RLUnloaded",1)
		MISR KLM 1
		RLNG A 0 A_PlaySound("rocket/reload2")
        MISR NO 1
		MISR P 4
		MISR QR 1

	InsertBullets:
		TNT1 AAAAAA 0
		RLNG A 0 A_JumpIfInventory("RocketRounds",6,"FinishReload")
		RLNG A 0 A_JumpIfInventory("AmmoRocket",1,1)
		Goto Ready
        TNT1 AAAAAA 0
		RLNG A 0 A_Takeinventory("RLUnloaded",1)
		RLNG A 0 A_Giveinventory("RocketRounds",1)
		RLNG A 0 A_Takeinventory("AmmoRocket",1)
		Goto InsertBullets

	FinishReload:
		TNT1 AAAA 0
		RLNG A 1 A_Takeinventory("Reloading",1)
        RLNG A 0 A_JumpIfInventory("FiredPrimary",1,"Fire")
        Goto Ready

	Spawn:
		RLAU A -1
		Stop

	Unload:
		RLNG A 0
		TNT1 A 0 A_JumpIfInventory("RLUnloaded",1,"UnloadCancel")
		RLNG A 1 A_WeaponReady
		RLNG A 0 A_Takeinventory("Unloading",1)
		A12G A 0 A_Takeinventory("ADSmode",1)
		RLNG A 0 A_Takeinventory("Zoomed",1)
		RLNG A 0 A_Takeinventory("PowerLightAmp", 1)
        RLNG A 0 A_ZoomFactor(1.0)
		RLNG A 0 A_SetCrosshair(0)
		RLNG A 0 A_JumpIfInventory("RocketRounds",1,1)
		Goto ReadyNoAmmo
        TNT1 AAAA 0

        MISR ABCDEF 1 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
		A12R A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		MISR G 4 A_WeaponReady(WRF_NOBOB | WRF_NOFIRE)
		A12R A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		RLNG A 0 A_PlaySound("rocket/reload1")
		A12G A 0 A_GiveInventory("RLUnloaded",1)
        MISR HI 1
		MISR J 8

	FinishUnload:
		TNT1 AAAAAA 0
		RLNG A 0 A_JumpIfInventory("RocketRounds",1,1)
		Goto ReadyNoAmmo
        TNT1 AAAA 0
		RLNG A 0 A_Takeinventory("RocketRounds",1)
		RLNG A 0 A_Giveinventory("AmmoRocket",1)
		Goto FinishUnload

	UnloadCancel:
		MISR J 1 A_WeaponReady
		A12G A 0 A_Takeinventory("Unloading",1)
		A12G A 0 A_Takeinventory("ADSmode",1)
		A12G A 0 A_Takeinventory("Zoomed",1)
		RLNG A 0 A_Takeinventory("PowerLightAmp", 1)
        RLNG A 0 A_ZoomFactor(1.0)
		RLNG A 0 A_SetCrosshair(0)
		Goto ReadyNoAmmo

	 AltFire:
        RLNG A 0
		RLNG A 0 A_JumpIfInventory("Zoomed",1,"NoAim")
		RLNG A 0 A_Giveinventory("Zoomed",1)
        RLNG A 0 A_ZoomFactor(3.0)
		RLNG A 0 A_giveinventory("PowerLightAmp", 1)
		RLNG A 0 A_SetCrosshair(41)
		TNT1 A 0 A_PlaySound("ads/in")
        MIAI ABCDE 1
        RLNG A 0 A_JumpIfInventory("FiredPrimary",1,"Fire")
		SNIP A 1
		RLNG A 0 A_JumpIfInventory("FiredPrimary",1,"Fire")
		SNIP A 1
		RLNG A 0 A_JumpIfInventory("FiredPrimary",1,"Fire")
		SNIP A 1
		RLNG A 0 A_JumpIfInventory("FiredPrimary",1,"Fire")
		SNIP A 1
		RLNG A 0 A_JumpIfInventory("FiredSecondary",1,"HoldAim")
        Goto Ready2

	HoldAim:
	    RLNG A 0 A_JumpIfInventory("FiredPrimary",1,"Fire")
		SNIP A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		RLNG A 0 A_JumpIfInventory("FiredPrimary",1,"Fire")
		SNIP A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		RLNG A 0 A_JumpIfInventory("FiredPrimary",1,"Fire")
		SNIP A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		RLNG A 0 A_JumpIfInventory("FiredPrimary",1,"Fire")
		SNIP A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		RLNG A 0 A_JumpIfInventory("FiredSecondary",1,"HoldAim")

	 NoAim:
		RLNG A 0
		RLNG A 0 A_Light0
		RLNG A 0 A_Takeinventory("Zoomed",1)
		RLNG A 0 A_Takeinventory("PowerLightAmp", 1)
        RLNG A 0 A_ZoomFactor(1.0)
		RLNG A 0 A_SetCrosshair(0)
		TNT1 A 0 A_PlaySound("ads/out")
        MIAI EDCBA 1
        Goto Ready
	}
}

ACTOR RocketRounds : Ammo
{
   Inventory.Amount 0
   Inventory.MaxAmount 6
   Ammo.BackpackAmount 0
   Ammo.BackpackMaxAmount 6
   Inventory.Icon "RLAUA0"
}
